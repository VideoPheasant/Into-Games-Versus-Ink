->Vending_Machine_Entrance
===Vending_Machine_Entrance===

{The patented dimension corp vending machine is completely mundane unless a discerning customer like yourself looks at it very closely. These are not normal products. These are not normal buttons.|The vending machine waits hungrily for you to make another selection. Go on. Treat yourself. Open yourself up to new experiences.}

+{not Thomas_Button.Pushed}Sister Tilly Sizzle's Slightly Too Spicy Slices!
->Thomas_Button

*Lucy's Vending Machine Button.
->Lucy_Button

+Oobleck Enterprise's Non-Newtonian Nutrition
->Lyra_Button

+{not Teo_Button.ForcefullyPressAgain}Sir Jerry Breen's Fantastical Jellybeans
->Teo_Button

*Jon's Vending Machine Button.
->Jon_Button

*Kyle's Vending Machine Button.
->Kyle_Button

+{not Jenyth_Button.Pushed}One button... seems to be made of cork.
->Jenyth_Button



===Thomas_Button===

The eponymous & iconic Tilly Sizzle is depicted on the logo for TSSTSS in her usual form. That of a cartoon laser pistol with eyelashes. She was a more appropriate mascot back when her company sold weapons, but she seems happier now they've moved on to snacks. She still has that far off look in her eyes though.

Tilly's slices come with an absolute guarantee that they will be slightly too spicy for the consumer. You'd have to be pretty hardcore to pick something that you're guaranteed not to enjoy.

*[Push the button because you're hardcore.]
->Pushed
*[Push the button because you're curious.]
->Pushed
+Leave the button alone for now.
->Vending_Machine_Entrance

=Pushed

The vending machine shudders and smokes slightly before a dry foil package drops into its pick-up box with a light crinkling sound. You can smell it already, without opening it. Seems intimidatingly spicy.

The packaging is almost entirely disclaimer text printed on an image of Tilly Sizzle's face. She looks concerned.

*[Read the disclaimer.]

<i>WARNING. THIS PRODUCT CONTAINS GLUTEN, PEANUTS, QUANTUM WAVEFORM RECEIVERS AP-105,AP-103,AP-5^787,AP-0000,AP-DOROTHY.</i>

<i>Depending on your physiology and disposition the collapse of these quantum waveforms is guaranteed to and in some cases has already caused negative symptoms ranging from DISCOMFORT to INSTANT DEATH.</i>

<i>This product is not suitable for consumption by Highly Improbable Entities or Wusses. This product is not suitable for enjoyment by anyone.</i>

    **[On second thought... No.]
    
        The packaging senses your hesitation, and the look in Tilly Sizzle's eyes seems to shift from haunted worry to bitter disappointment. Too spicy for you, is it? The package heats up rapidly in your hand until it's slightly too hot to hold and you drop it with a yelp. It cooks itself to ash right there on the floor in front of you. The thick smoke wafting from the torched packaging carries complex layers of pepper and caraway. It makes your mouth water.
        ->Vending_Machine_Entrance
    
    **[They only write this stuff to make you feel cool for eating something so dangerous... and it's working...]
    ->Eaten
    
    **[This is a rare opportunity to eat something with QUANTUM WAVEFORM RECEIVERS... whatever those are. Better not pass it up.]
    ->Eaten

*[Just tear open the bag and give them a try. Hopefully someone will see you eating them and be impressed.]
->Eaten

=Eaten

The slices themselves are light brown unassuming discs that remind you of dried apricot. You drop one onto the palm of your hand to examine it, and almost immediately your hand begins to tingle. The spice is on your skin and it's a little bit much. You instinctively pop the slice in your mouth.

It tastes incredible. It's savoury. It's complex. It's sweet but also mature. It's earthy but also refreshing. It's... Oh here comes the spice. No. No. It's too spicy. It's just a little bit too spicy to enjoy it really.

*[Spit it out.]

    You splutter and spit it out.

    
*[Just bear it. Force yourself to chew.]

No. No. It's really <i>too</i> spicy. You try to inhale and the air passing over the slice becomes hostile to all human life on its way down your throat. You splutter and just like that you've lost it.

-

The slightly too spicy slice slops onto the ground trailing smoke like the tail of a slimy comet.

The packaging seems to sense your rejection and heats up rapidly in your hand until it's slightly too hot to hold and you drop it with a yelp. It cooks itself to ash right there on the floor in front of you. The thick smoke wafting from the torched packaging makes your mouth water, but you know it's actually too spicy to enjoy.
    ->Vending_Machine_Entrance


===Lucy_Button===
->Vending_Machine_Entrance

===Lyra_Button===
The button is slightly sticky to the touch.
*Press it
->Sticky_Button

+Reconsider your choice
->Vending_Machine_Entrance


===Sticky_Button===
- The button yields to your touch - it feels a lot like a poking your fingertips into a pot of homemade slime.

The vending machine's speakers crackle into life.

"Thank you for choosing Oobleck Enterprise's non-Newtonian products - the most efficient way to delight your tastebuds while meeting your base nutritional needs.

Please select an option from our curated selection of exceptionally adequate flavours."
*Press the green button
->Green_Button

*Press the red button
->Red_Button

*Press the grey button
->Grey_Button

*Try something else
->Vending_Machine_Entrance


===Green_Button===
- The machine whirs - there's a muffled <i>thud</i> as a small gelatinous cube falls into the open vending slot.

*Pick it up
->Pick_Green_Cube

*Yeah, no thanks
->Vending_Machine_Entrance

===Pick_Green_Cube===
-You pick up the green cube and inspect it. It looks like ordinary jelly, but feels denser. There are small specks of what you assume to be some kind of ground-up leaf suspended inside it.

*Eat the green cube
->Eat_Green_Cube

*Try something else
->Vending_Machine_Entrance

===Eat_Green_Cube===
- It tastes... minty, with a faint floral undertone. It's surprisingly pleasant.

*Try something else
->Vending_Machine_Entrance

===Red_Button===
- Something plops unceremoniously into the open vend slot. 

*Pick it up
->Pick_Red_cube

*Have second thoughts
->Vending_Machine_Entrance

===Pick_Red_cube===
- It looks like a cube of ordinary jelly. Its colour evokes synethetic strawberry flavours.

*Eat the red cube
->Eat_Red_Cube

*Try something else
->Vending_Machine_Entrance

===Eat_Red_Cube===
- It takes a moment for you to place the savory, metallic taste. The cube bursts in your mouth, releasing a creamy fondant centre. It's the taste of an extremely rare steak combined with the texture of a soufflé.

Was that... blood flavoured?

*Try something else
->Vending_Machine_Entrance

===Grey_Button===
- You pick up what appears to be fine grey powder compressed into a cube. Some of it crumbles away and sticks to your fingertips.

*Eat the grey cube
->Eat_Grey_Cube

*Decide against it
->Vending_Machine_Entrance

===Eat_Grey_Cube===
- It's sweet and delicious, like eating powdered cookies and cream. You're struck by the immediate urge to eat a dozen more of them.

*Try something else
->Vending_Machine_Entrance


===Teo_Button===

The name "Sir Jerry Breen's Fantastical Jellybeans" is one you are very familiar with. It is one of the most infamous, and exorbitantly expensive candies in the multi-verse. Despite their unreasonable pricing, the jellybeans still see wide use. In fact they are dubbed the "perfect jellybeans".

+[Today is a special occasion, you can have some.]
->HaveSome
*[On second thought, I'd like to try something else.]
->Vending_Machine_Entrance

=HaveSome
Your gaze falls on the green, square button with the iconic figure of "Sir Jerry Breen" appearing to be worn out. Only one last bag of jellybeans sits in the vending machine. You press the button. It feels soft and dampened.

It doesn't seem to work.
+[Press the button again.]
->PressAgain
*[Don't bother, it's probably broken.]
->Vending_Machine_Entrance

=PressAgain
You press the button. It softly slides back inside, and out...

+[Forcefully press the button again.]
->ForcefullyPressAgain
*[Give Up.]
->Vending_Machine_Entrance

=ForcefullyPressAgain
Unexpectedly... The vending machine responds to your selection with a soulless, automated message sounding from its speaker:

"Sir Jerry Breen's Fantastical Jellybeans are sure to delight you and sure not to disappoint!"

The vending machine clacks and whirs, struggling to push your item in the drop box.

*[Give it a minute.]
->GiveItAMinute
*[Give it a good shake (like in the movies).]
->GiveItAShake

=GiveItAMinute
Patience is a virtue, you think to yourself.
*[Wait a bit longer.]
->Wait

=Wait
Catching yourself dozing off, you snap back to reality, finding the jellybeans bag in the drop box. 

You enthusiastically pick them up.
*[Read the item's description.]
->ReadDescription1
*[You can't wait. You need to have a jellybean right now.]
->HaveAJellybean

=GiveItAShake
Hoping someone watches, you put on a performance. You bump into the vending machine, shaking it, again, and again, until the plastic rustling against the metal and the final thud of the drop cap off the show.

You proudly look around to find out not a soul turned to pay attention. You feel a bit embarrassed, but nonetheless brush it off and pick up the jellybeans bag.
*[Read the item's description.]
->ReadDescription1
*[You can't wait. You need to have a jellybean right now.]
->HaveAJellybean


=ReadDescription1
The tale of our founder Sir Jerry Breen is well-known across the multi-verse. A tale of an inventor, a saviour, and a peacemaker. His “fantastical” jellybeans united the multi-verse after millennia of war and devastation. Where there was nothing but difference and division, Sir Jerry Breen showed the world that there was much more in common, and where differences remained, his jellybeans more than made up for.
*[Continue reading.]
->ReadDescription2
*[Enough with reading, a jellybean is in order.]
->HaveAJellybean

=ReadDescription2
"Sir Jerry Breen's Fantastical Jellybeans" don't have any taste to begin with, but once they come into contact with saliva they perform a rapid, harmless analysis of the organism's genetic make-up. The result? Flavours adapt to your unique preferences, making them truly countless, and colours can range far beyond the visible light spectrum often making them hardly visible to the naked human eye.

Have these jellybeans at your discretion, as once you've tasted perfection, there's no going back.
*[It's jellybean time!]
->HaveAJellybean

=HaveAJellybean
You swiftly tear the bag open in one clean cut.

You gaze inside, and see a sea of shining colour, jellybeans ranging from ruby red, emerald green, diamond blue, golden yellow, even to silvery see-through, colours far beyond your eyes allow you to discern.

You close your eyes and give them a good shuffle. You sink your fingers deep in the bag, and pick out...

A {~Ruby Red|Emerald Green|Diamond Blue|Golden Yellow|Silvery See-through} jellybean!

You playfully toss it in your mouth and chew. The jellybean initially leaves you unimpressed, tasting bland.As you continue chewing, you gradually start to sense a new flavour, one that words can't describe. It's unlike the taste of a fruit, chocolate, or sugary drink. It's a novelty, a flavour unique to you and one that no other has experienced.

You continue chewing and enjoying the jellybean...

->Vending_Machine_Entrance

===Jon_Button===
->Vending_Machine_Entrance

===Kyle_Button===
The button glows with a dizzying light not suitable for mortal eyes. Bizarre glyphs flicker in and out of view around the button while the sound of ambient guttural chanting (or is it whispers) can be heard the longer one looks.
*Listen closely.
->Listen
*Push the button.
->Pushed
*Don't push the button.
->Vending_Machine_Entrance

=Listen
"[Insert a catchy jingle about forbidden knowledge in a tasty bite-sized treat]. Trying to harness long forgotten powers wielded by ancient beings so grand in scale that the very idea of them might cause unsolicited madness? Try <i>incomprehensible word</i>! <i>incomprehensible word</i> packs HUGE flavor and CENTURIES worth of occult wisdom in every bite! No rituals necssary, no tentacles, and no chance of pesky mutations! Try <i>incomprehensible word</i>, it's beyond belief!"

=Pushed
The vending machine trembles as ancient magic is beckoned, threatening to sunder the skeins of reality and call unspeakable abominations through its dispenser, before it ejects a holographic package decorated with the unassuming black logo of an eye. The stars begin to align at this singular cosmic focal point. You notice a small notch at the upper left side of the foil package beckoning - no, demanding - you cross the precipice of destiny and receive that which only those blessed by the Ancients could ever hope to obtain.
*Open the package.
->Opened

=Opened
A thought stirs within you, suggesting that every moment in your life has led to this. Did you choose the Button, or did the Button choose you? You open the package and empty the contents into the palm of your hand. The singular contents of the package share more in common with a raisin than anything your imagination might have conjured. Well, at least it's not tentacles.

*Eat it.
->Eaten

=Eaten
You pop the dried, leathery snack into your mouth and chew, releasing the flavors trapped within. At first, there is the sweetness of an apricot, though the texture feels more like a date. The flavor begins to transform, undergoing some kind of chemical metamorphosis as your taste buds struggle to make sense of what you've introduced them to. Sweet becomes tart becomes...something else entirely. A sensation of etheriality washes over you as you swallow, and the effects of the snack take hold. Your vision softens. Your breathing relaxes. And, at the climax of your mortal form, having tasted the fruit of the gods, you begin to see. The spaces that surround you are merely concepts, and secret passages reveal themselves. Walls become doors and doors become windows into new realms. Euphoria takes hold of you as your perception transcends into a metaphysical state.     

*Don't open the package.
=Unopened
A sense of overwhelming dread grips you. Maybe it would be better to open this later.

->Vending_Machine_Entrance

===Jenyth_Button===
VAR correctFrench = 0
VAR incorrectFrench = 0
VAR likesWine = true

LIST WineTypes = (red), (white), (orange), (silver)
VAR currentWineType = ""

LIST RedWineTypes = shiraz, merlot, cabernetSauvignon
LIST WhiteWineTypes = chardonnay, savignonBlanc

LIST OrangeWineTypes = goûtDeGirofle
LIST SilverWineTypes = boleau, pinotÉtoilé

LIST NegativeWineNouns = plonk, corkedRubbish, swill
LIST PositiveWineAdjectives = special, interesting

LIST NotesNoun = (citrus), (peach), (melon), (battery), (fig), (sharpie), (cardboard), (stardust), (tyre), (horse), (petrol)
LIST BodyAdjectives = angular, full, flabby, approachable, balanced, crisp, dense, supple, sticky, slick
LIST FlavourAdjectives = dry, earthy, elegant, herbaceous, jammy, savoury, silky, spicy, tart, unctuous, zesty, peppery

 
 {On further inspection, it seems like a cork bottlestop has been shoved into a button slot. Perhaps a little unceremoniously.|The cork button is still there. {not Jenyth_Button.Pushed}Unexplained.}
 
 * Does it even say anything?
 ->Read_Button
 *[P{|erhaps now you'll p}ress the button.]
 -> Pushed
 *[Look at something else...]
-> Vending_Machine_Entrance

 =Read_Button
It bears a faded ink insignia: <i>MSN de POMP. VINS</i>.
->Jenyth_Button

 =Pushed
Something clicks, then whirrs within the machine. And then a faint loop of static, before a tinny voice pipes up.

<i>Ah, bonjour, my dear cus-de-mér!</i>

*[Look at the other buttons. Anything but whatever this is.]
->Vending_Machine_Entrance
*[Hello?]
->Intro
*[Bonjour?]
    ~ correctFrench++
    <i>Ahon, you are... fluent!</i> Wherever the voice is coming from, it hesitates for a second. Clears its throat.
    ->Intro
    
=Intro
{correctFrench > 0:
    <i>Mais, I digress... bienvenue en le Maison de Pomponville!</i>
- else:
    <i>Alors: bienvenue en le Maison de Pomponville!</i>
}
    
The voice gains a certain glassy-eyed quality, as if reading from an autocue. <i>We are the home of the finest of wines. We 'ave you covered, no matter your taste: old worlds, new worlds, we 'ave them all!</i>

*[Remain silent as a sign of assent.]
    <i>Ah, I can tell you are a... how you say. Person of great taste!</i> The voice descends into a series of guffaws, noises you can only imagine being made by someone swilling a glass of wine around at a dinner party.
*[Remain silent and thoroughly unimpressed.]
    A nervous crackle of laughter, and a few extra clicks. Then, the voice continues.
- 
<i>Well, uh, would you 'ave any vin to ensample?</i>

*[Oui.]
~ correctFrench++
->Order_Wine
*[No.]
->Exit_Button
*[Non.]
~ correctFrench++
->Exit_Button

=Order_Wine

// choose a random wine type from the list to be served
~ currentWineType = LIST_RANDOM(WineTypes)
{LIST_COUNT(WineTypes) > 0:
{<i>Sur bien!</i> The vending machine vibrates gently, then ends with a sudden record scratch. The familiar static returns.|Another judder and hum from the machine.}

{A small hatch on the front opens, revealing a spout not unlike an automatic coffee machine. A scratched |Another} plastic {wine glass falls down from above, and as teeters in place, {currentWineType} wine gushes out of the spout to fill it.| glass precariously drops. But this time, it is filled with some {currentWineType} wine.} 

<i>{Now, here is something|And here, this bev-vér-age is|I am certain that this time, this will be} very special.</i> A pause.
}
~ WineTypes -= currentWineType
{
- currentWineType == red:
    ~ RedWineTypes = ()
    ~ RedWineTypes += LIST_RANDOM(LIST_ALL(RedWineTypes))
    <i>A {nameOfRedWine(RedWineTypes)}.</i>

- currentWineType  == white:
    ~ WhiteWineTypes = ()
    ~ WhiteWineTypes += LIST_RANDOM(LIST_ALL(WhiteWineTypes))
    <i>A {nameOfWhiteWine(WhiteWineTypes)}.</i>

- currentWineType == orange:
    ~ OrangeWineTypes = ()
    ~ OrangeWineTypes += LIST_RANDOM(LIST_ALL(OrangeWineTypes))
    <i>A {nameOfOrangeWine(OrangeWineTypes)}.</i>

- currentWineType == silver:
    ~ SilverWineTypes = ()
    ~ SilverWineTypes += LIST_RANDOM(LIST_ALL(SilverWineTypes))
    <i>A {nameOfSilverWine(SilverWineTypes)}.</i>

- else:
~ likesWine = false
The voice has gained a new quality: as if you can hear the sweat running down its brow. <i>Mais, my dear cus-de-mér... we 'ave no other types of wine.</i>
}
+{likesWine}[But I don't like {currentWineType} wine.]
->Reorder_Wine
+{currentWineType == silver || currentWineType == orange}[What even... is {currentWineType}?]
->Reorder_Wine
*{likesWine}[You bring the glass to your lips.]
->Taste_Wine

*{!likesWine}[Maybe you just don't like wine.]
->Exit_Button

=Reorder_Wine

<i>{Ah, mais of course!</i> A nervous chortle. <i>Le cus-de-mér is always right. If you please, just throw that, uh...</i>|Je suis</i> so <i>sorry, my dear cus-dé-mer: please, throw that|Once again, I can only apologise: fling that} {nameOfNegativeWineNouns(LIST_RANDOM(LIST_ALL(NegativeWineNouns)))} <i> on le floor. {Where it belongs.</i>|}
-> Order_Wine

=Taste_Wine
VAR firstNoteNoun = ()
~ firstNoteNoun = LIST_RANDOM(LIST_ALL(NotesNoun))

Hmm... how would you describe this? Notes of... {firstNoteNoun}, and {LIST_RANDOM(LIST_ALL(NotesNoun))}. You might say the body feels {LIST_RANDOM(LIST_ALL(BodyAdjectives))}.

As it sits on your palate, it develops a new flavour. Something {LIST_RANDOM(LIST_ALL(FlavourAdjectives))}. Then, {LIST_RANDOM(LIST_ALL(FlavourAdjectives))}.

<i>Ahhh... my dear cus-de-mér, 'ow are you liking your wine? The tannins, delicious, no?</i>

*[Time to line your stomach with something else.]
->Exit_Button

=Exit_Button
The voice seems to sense your eyes drifting to other buttons. <i>Ah, o-kay, c'est... fine, mais, I do get paid by the number of reviews on our website, so please do-</i>

But suddenly, a pop, static, then a click. Silence.

The voice is gone.

->Vending_Machine_Entrance

=== function nameOfWhiteWine(what)
{ what:
    - savignonBlanc: savignon blanc
    - chardonnay: chardonnay
}

=== function nameOfRedWine(what)
{ what:
    - shiraz: shiraz
    - merlot: merlot
    - cabernetSauvignon: cabernet sauvignon
}

=== function nameOfOrangeWine(what)
{ what:
    - goûtDeGirofle: goût de girofle
}

=== function nameOfSilverWine(what)
{ what:
    - boleau: boleau
    - pinotÉtoilé: pinot étoilé
}

=== function nameOfNegativeWineNouns(what)
{ what:
    - corkedRubbish: corked rubbish
    - plonk: plonk
    - swill: swill
}
