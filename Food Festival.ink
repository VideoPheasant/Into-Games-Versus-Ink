->Vending_Machine_Intro
=Vending_Machine_Intro

Everyone remembers the first time they tasted The Fare. For you, your first true meal was<>

 *...the cooking apple[...], nothing like it promised to be. Taste of the warning label on a battery. Taste of every single thing's skin. Taste of
 *...the IV[...], taste of metal, and heart beats, and time passing without asking you. Taste of
 *...the cup of tea[...] before your first words. The taste of the words you babbled when you copied the adults. Taste of
 *...grandad's horrendous scones[...] and the sludgy raspberries and the complete lack of sugar. Taste of

- <> The Fare. 

Cold in the nose. Crowded and smouldering out there somewhere in every kitchen and every stomach. In every hand and in every mouth. The taste of The Fare suddenly everywhere.->Vending_Machine_Intro2

=Vending_Machine_Intro2

*In the sausage roll eaten quickly and alone, amongst fancy restaurants.[..] The kinship with vermin, and everyone else. ->Vending_Machine_Intro2

*In the rising charcoal smell from your downstairs neighbour, who isn't well, and has forgotten his cooking.[..] Knocking on his door absolutely certain that The Fare awaits you on the other side.->Vending_Machine_Intro2

*In the chewing gum you swallowed during your first kiss.[..] Your hands in someone else's pockets, reaching out for something nostalgic that you've never tasted.->Vending_Machine_Intro2

*->Vending_Machine_Intro3


=Vending_Machine_Intro3
<br>
In all of it a piece of The Fare, gathered, tasted, and treasured. In all of the pieces, something more than a piece.

*A key.

*A gateway.

*An idea.

-

*A vending machine. 

-

It looms in your mind. The culmination of a vivid gastromantic meditation that has taken a lifetime.

*It's a door.

*It's a hunger.

-

It swims sarcastically in your mind. It's an invitation to The Fare. It doesn't like you. It shares nothing common with you, but it wants to feed you. If you want to taste The Fare, you will have to start here.

->Vending_Machine_Entrance
===Vending_Machine_Entrance===

{The buttons and products lack the gravitas and romance you were expecting. A test, maybe?|The vending machine waits hungrily for you to make another selection. Go on. Treat yourself. Open yourself up to new experiences.}

+{not Thomas_Button.Pushed}Sister Tilly Sizzle's Slightly Too Spicy Slices!
->Thomas_Button

*Lucy's Vending Machine Button.
->Lucy_Button

+Oobleck Enterprise's Non-Newtonian Nutrition
->Lyra_Button

+{not Teo_Button.ForcefullyPressAgain}Sir Jerry Breen's Fantastical Jellybeans
->Teo_Button

*Jon's Vending Machine Button.
->Jon_Button

*Kyle's Vending Machine Button.
->Kyle_Button

+{not Jenyth_Button.Pushed}One button... seems to be made of cork.
->Jenyth_Button



===Thomas_Button===

The eponymous & iconic Tilly Sizzle is depicted on the logo for TSSTSS in her usual form. That of a cartoon laser pistol with eyelashes. She was a more appropriate mascot back when her company sold weapons, but she seems happier now they've moved on to snacks. She still has that far off look in her eyes though.

Tilly's slices come with an absolute guarantee that they will be slightly too spicy for the consumer. You'd have to be pretty hardcore to pick something that you're guaranteed not to enjoy.

*[Push the button because you're hardcore.]
->Pushed
*[Push the button because you're curious.]
->Pushed
+Leave the button alone for now.
->Vending_Machine_Entrance

=Pushed

The vending machine shudders and smokes slightly before a dry foil package drops into its pick-up box with a light crinkling sound. You can smell it already, without opening it. Seems intimidatingly spicy.

The packaging is almost entirely disclaimer text printed on an image of Tilly Sizzle's face. She looks concerned.

*[Read the disclaimer.]

<i>WARNING. THIS PRODUCT CONTAINS GLUTEN, PEANUTS, QUANTUM WAVEFORM RECEIVERS AP-105,AP-103,AP-5^787,AP-0000,AP-DOROTHY.</i>

<i>Depending on your physiology and disposition the collapse of these quantum waveforms is guaranteed to and in some cases has already caused negative symptoms ranging from DISCOMFORT to INSTANT DEATH.</i>

<i>This product is not suitable for consumption by Highly Improbable Entities or Wusses. This product is not suitable for enjoyment by anyone.</i>

    **[On second thought... No.]
    
        The packaging senses your hesitation, and the look in Tilly Sizzle's eyes seems to shift from haunted worry to bitter disappointment. Too spicy for you, is it? The package heats up rapidly in your hand until it's slightly too hot to hold and you drop it with a yelp. It cooks itself to ash right there on the floor in front of you. The thick smoke wafting from the torched packaging carries complex layers of pepper and caraway. It makes your mouth water.
        ->Vending_Machine_Entrance
    
    **[They only write this stuff to make you feel cool for eating something so dangerous... and it's working...]
    ->Eaten
    
    **[This is a rare opportunity to eat something with QUANTUM WAVEFORM RECEIVERS... whatever those are. Better not pass it up.]
    ->Eaten

*[Just tear open the bag and give them a try. Hopefully someone will see you eating them and be impressed.]
->Eaten

=Eaten

The slices themselves are light brown unassuming discs that remind you of dried apricot. You drop one onto the palm of your hand to examine it, and almost immediately your hand begins to tingle. The spice is on your skin and it's a little bit much. You instinctively pop the slice in your mouth.

It tastes incredible. It's savoury. It's complex. It's sweet but also mature. It's earthy but also refreshing. It's... Oh here comes the spice. No. No. It's too spicy. It's just a little bit too spicy to enjoy it really.

*[Spit it out.]

    You splutter and spit it out.

    
*[Just bear it. Force yourself to chew.]

No. No. It's really <i>too</i> spicy. You try to inhale and the air passing over the slice becomes hostile to all human life on its way down your throat. You splutter and just like that you've lost it.

-

The slightly too spicy slice slops onto the ground trailing smoke like the tail of a slimy comet.

The packaging seems to sense your rejection and heats up rapidly in your hand until it's slightly too hot to hold and you drop it with a yelp. It cooks itself to ash right there on the floor in front of you. The thick smoke wafting from the torched packaging makes your mouth water, but you know it's actually too spicy to enjoy.
    ->Vending_Machine_Entrance


===Lucy_Button===
->Vending_Machine_Entrance

===Lyra_Button===
The button is slightly sticky to the touch.
*Press it
->Sticky_Button

+Reconsider your choice
->Vending_Machine_Entrance


===Sticky_Button===
- The button yields to your touch - it feels a lot like a poking your fingertips into a pot of homemade slime.

The vending machine's speakers crackle into life.

"Thank you for choosing Oobleck Enterprise's non-Newtonian products - the most efficient way to delight your tastebuds while meeting your base nutritional needs.

Please select an option from our curated selection of exceptionally adequate flavours."
*Press the green button
->Green_Button

*Press the red button
->Red_Button

*Press the grey button
->Grey_Button

*Try something else
->Vending_Machine_Entrance


===Green_Button===
- The machine whirs - there's a muffled <i>thud</i> as a small gelatinous cube falls into the open vending slot.

*Pick it up
->Pick_Green_Cube

*Yeah, no thanks
->Vending_Machine_Entrance

===Pick_Green_Cube===
-You pick up the green cube and inspect it. It looks like ordinary jelly, but feels denser. There are small specks of what you assume to be some kind of ground-up leaf suspended inside it.

*Eat the green cube
->Eat_Green_Cube

*Try something else
->Vending_Machine_Entrance

===Eat_Green_Cube===
- It tastes... minty, with a faint floral undertone. It's surprisingly pleasant.

*Try something else
->Vending_Machine_Entrance

===Red_Button===
- Something plops unceremoniously into the open vend slot. 

*Pick it up
->Pick_Red_cube

*Have second thoughts
->Vending_Machine_Entrance

===Pick_Red_cube===
- It looks like a cube of ordinary jelly. Its colour evokes synethetic strawberry flavours.

*Eat the red cube
->Eat_Red_Cube

*Try something else
->Vending_Machine_Entrance

===Eat_Red_Cube===
- It takes a moment for you to place the savory, metallic taste. The cube bursts in your mouth, releasing a creamy fondant centre. It's the taste of an extremely rare steak combined with the texture of a soufflé.

Was that... blood flavoured?

*Try something else
->Vending_Machine_Entrance

===Grey_Button===
- You pick up what appears to be fine grey powder compressed into a cube. Some of it crumbles away and sticks to your fingertips.

*Eat the grey cube
->Eat_Grey_Cube

*Decide against it
->Vending_Machine_Entrance

===Eat_Grey_Cube===
- It's sweet and delicious, like eating powdered cookies and cream. You're struck by the immediate urge to eat a dozen more of them.

*Try something else
->Vending_Machine_Entrance


===Teo_Button===

The name "Sir Jerry Breen's Fantastical Jellybeans" is one you are very familiar with. It is one of the most infamous, and exorbitantly expensive candies in the multi-verse. Despite their unreasonable pricing, the jellybeans still see wide use. In fact they are dubbed the "perfect jellybeans".

+[Today is a special occasion, you can have some.]
->HaveSome
*[On second thought, I'd like to try something else.]
->Vending_Machine_Entrance

=HaveSome
Your gaze falls on the green, square button with the iconic figure of "Sir Jerry Breen" appearing to be worn out. Only one last bag of jellybeans sits in the vending machine. You press the button. It feels soft and dampened.

It doesn't seem to work.
+[Press the button again.]
->PressAgain
*[Don't bother, it's probably broken.]
->Vending_Machine_Entrance

=PressAgain
You press the button. It softly slides back inside, and out...

+[Forcefully press the button again.]
->ForcefullyPressAgain
*[Give Up.]
->Vending_Machine_Entrance

=ForcefullyPressAgain
Unexpectedly... The vending machine responds to your selection with a soulless, automated message sounding from its speaker:

"Sir Jerry Breen's Fantastical Jellybeans are sure to delight you and sure not to disappoint!"

The vending machine clacks and whirs, struggling to push your item in the drop box.

*[Give it a minute.]
->GiveItAMinute
*[Give it a good shake (like in the movies).]
->GiveItAShake

=GiveItAMinute
Patience is a virtue, you think to yourself.
*[Wait a bit longer.]
->Wait

=Wait
Catching yourself dozing off, you snap back to reality, finding the jellybeans bag in the drop box. 

You enthusiastically pick them up.
*[Read the item's description.]
->ReadDescription1
*[You can't wait. You need to have a jellybean right now.]
->HaveAJellybean

=GiveItAShake
Hoping someone watches, you put on a performance. You bump into the vending machine, shaking it, again, and again, until the plastic rustling against the metal and the final thud of the drop cap off the show.

You proudly look around to find out not a soul turned to pay attention. You feel a bit embarrassed, but nonetheless brush it off and pick up the jellybeans bag.
*[Read the item's description.]
->ReadDescription1
*[You can't wait. You need to have a jellybean right now.]
->HaveAJellybean


=ReadDescription1
The tale of our founder Sir Jerry Breen is well-known across the multi-verse. A tale of an inventor, a saviour, and a peacemaker. His “fantastical” jellybeans united the multi-verse after millennia of war and devastation. Where there was nothing but difference and division, Sir Jerry Breen showed the world that there was much more in common, and where differences remained, his jellybeans more than made up for.
*[Continue reading.]
->ReadDescription2
*[Enough with reading, a jellybean is in order.]
->HaveAJellybean

=ReadDescription2
"Sir Jerry Breen's Fantastical Jellybeans" don't have any taste to begin with, but once they come into contact with saliva they perform a rapid, harmless analysis of the organism's genetic make-up. The result? Flavours adapt to your unique preferences, making them truly countless, and colours can range far beyond the visible light spectrum often making them hardly visible to the naked human eye.

Have these jellybeans at your discretion, as once you've tasted perfection, there's no going back.
*[It's jellybean time!]
->HaveAJellybean

=HaveAJellybean
You swiftly tear the bag open in one clean cut.

You gaze inside, and see a sea of shining colour, jellybeans ranging from ruby red, emerald green, diamond blue, golden yellow, even to silvery see-through, colours far beyond your eyes allow you to discern.

You close your eyes and give them a good shuffle. You sink your fingers deep in the bag, and pick out...

A {~Ruby Red|Emerald Green|Diamond Blue|Golden Yellow|Silvery See-through} jellybean!

You playfully toss it in your mouth and chew. The jellybean initially leaves you unimpressed, tasting bland.As you continue chewing, you gradually start to sense a new flavour, one that words can't describe. It's unlike the taste of a fruit, chocolate, or sugary drink. It's a novelty, a flavour unique to you and one that no other has experienced.

You continue chewing and enjoying the jellybean...

->Vending_Machine_Entrance

===Jon_Button===
->Vending_Machine_Entrance

===Kyle_Button===
The button glows with a dizzying light not suitable for mortal eyes. Bizarre glyphs flicker in and out of view around the button while the sound of ambient guttural chanting (or is it whispers) can be heard the longer one looks.
*Listen closely.
->Listen
*Push the button.
->Pushed
*Don't push the button.
->Vending_Machine_Entrance

=Listen
"[Insert a catchy jingle about forbidden knowledge in a tasty bite-sized treat]. Trying to harness long forgotten powers wielded by ancient beings so grand in scale that the very idea of them might cause unsolicited madness? Try <i>incomprehensible word</i>! <i>incomprehensible word</i> packs HUGE flavor and CENTURIES worth of occult wisdom in every bite! No rituals necssary, no tentacles, and no chance of pesky mutations! Try <i>incomprehensible word</i>, it's beyond belief!"

=Pushed
The vending machine trembles as ancient magic is beckoned, threatening to sunder the skeins of reality and call unspeakable abominations through its dispenser, before it ejects a holographic package decorated with the unassuming black logo of an eye. The stars begin to align at this singular cosmic focal point. You notice a small notch at the upper left side of the foil package beckoning - no, demanding - you cross the precipice of destiny and receive that which only those blessed by the Ancients could ever hope to obtain.
*Open the package.
->Opened

=Opened
A thought stirs within you, suggesting that every moment in your life has led to this. Did you choose the Button, or did the Button choose you? You open the package and empty the contents into the palm of your hand. The singular contents of the package share more in common with a raisin than anything your imagination might have conjured. Well, at least it's not tentacles.

*Eat it.
->Eaten

=Eaten
You pop the dried, leathery snack into your mouth and chew, releasing the flavors trapped within. At first, there is the sweetness of an apricot, though the texture feels more like a date. The flavor begins to transform, undergoing some kind of chemical metamorphosis as your taste buds struggle to make sense of what you've introduced them to. Sweet becomes tart becomes...something else entirely. A sensation of etheriality washes over you as you swallow, and the effects of the snack take hold. Your vision softens. Your breathing relaxes. And, at the climax of your mortal form, having tasted the fruit of the gods, you begin to see. The spaces that surround you are merely concepts, and secret passages reveal themselves. Walls become doors and doors become windows into new realms. Euphoria takes hold of you as your perception transcends into a metaphysical state.     

*Don't open the package.
=Unopened
A sense of overwhelming dread grips you. Maybe it would be better to open this later.

->Vending_Machine_Entrance

===Jenyth_Button===
VAR playerCorrectFrench = 0
VAR playerIncorrectFrench = 0
VAR voiceIncorrectFrench = 0
VAR currentWinesTasted = 0

VAR voiceKickedPlayerOut = false
VAR likesWine = true
VAR wantsDifferentWineColour = false
VAR wantsDifferentWineType = false
VAR correctedVoicesFrench = false
VAR ranOutOfWineTypes = false
VAR askedForDifferentTypeOfWine = false

LIST WineColours = (red), (white), (orange), (silver)
VAR currentWineColour = ()
VAR currentWineType = ()

LIST RedWineTypes = (shiraz), (merlot), (cabernetSauvignon)
LIST WhiteWineTypes = (chardonnay), (savignonBlanc)
LIST OrangeWineTypes = (goûtDeGirofle)
LIST SilverWineTypes = (boleau), (pinotÉtoilé)

LIST NegativeWineNouns = plonk, corkedRubbish, swill, rotgut
LIST PositiveWineAdjectives = special, interesting

LIST NotesNoun = (citrus), (peach), (melon), (battery), (fig), (sharpie), (cardboard), (stardust), (tyre), (horse), (petrol), (ripeBerries), (mocha), (blackPepper)
LIST BodyAdjectives = angular, full, flabby, approachable, balanced, crisp, dense, supple, sticky, slick
LIST FlavourAdjectives = dry, earthy, elegant, herbaceous, jammy, savoury, silky, spicy, tart, unctuous, zesty, peppery
LIST BarrelAdjectives = oak, cedar, birch, cherryWood, mahogany, pine, laminate, clay, sandstone
LIST RegionDescriptors = desert, ancientForest, swamp, greatPlains
LIST RegionAdverbs = diligently, cautiously, doggedly, triumphantly
LIST places = surrey, newKensington, lunarMoon, paradise
LIST TanninsAdjectives = (chewy), (grippy), (rough), (round), (soft), (structured), (euclidean)
LIST NumberOfYears = five = 5, ten = 10, fifteen = 15, twenty = 20, notSure, unfathomableNumber

//For now I want the list of adjectives and nouns here to reset every time. I couldn't be bothered putting brackets around every item in the list, so this autopopulates the list with every possible adjective defined within it when the player first enters the button.
~ NotesNoun = LIST_ALL(NotesNoun)
~ TanninsAdjectives = LIST_ALL(TanninsAdjectives)
~ FlavourAdjectives = LIST_ALL(FlavourAdjectives)
~ BarrelAdjectives = LIST_ALL(BarrelAdjectives)
~ NumberOfYears = LIST_ALL(NumberOfYears)

~ NotesNoun = LIST_ALL(NotesNoun)
 {On further inspection, it seems like a cork bottlestop has been shoved into a button slot. |The cork button is still there. {not Jenyth_Button.Pushed: Unexplained.}} 
 
 * [Does it say anything?]
 ->Read_Button
 *[P{|erhaps now you'll p}ress the button.]
 -> Pushed
 *[Look at something else...]
-> Vending_Machine_Entrance

 =Read_Button
It bears a faded ink insignia: <i>MSN de POMP. VINS</i>.
->Jenyth_Button

 =Pushed
Something clicks, then whirrs within the machine. And then a faint loop of static, before a tinny voice pipes up.

<i>Ah, bonjour, my dear cus-de-mér!</i>

*[Look at the other buttons. Anything but whatever this is.]
->Vending_Machine_Entrance
*<b>Hello?</b>
->Intro
*<b>Bonjour?</b>
    ~ playerCorrectFrench++
    <i>Ahon, a fellow... Français!</i> The voice laughs, but then hesitates for a second, leaving you alone with the static. Then it clears its throat.
    ->Intro
    
    
=Intro
{playerCorrectFrench > 0:
    <i>Mais, I digress... <>
- else:
    <i>Alors: <>
}
    bienvenue en la Maison de Pomponville!</i>
The voice gains a certain glassy-eyed quality, as if reading from an autocue. <i>We are the home of the finest of wines. We 'ave you covered, no matter your taste: old worlds, new worlds, worlds as of yet undiscovered: we 'ave them all!</i>

*Remain silent as a sign of assent.
    <i>Ah, I can tell you are a... how you say. Person of great taste!</i> The voice descends into a series of guffaws, noises you can only imagine being made by someone swilling a glass of wine around at a dinner party.
*Remain silent and thoroughly unimpressed.
    A nervous crackle of laughter, and a few extra clicks. Then, the voice continues.
*Hmm... something seems off about the voice's French.
    ~ correctedVoicesFrench = true
    You ponder what could be wrong.
    ** [Isn't <i>maison</i> masculine? Don't you mean <i>le maison</i>?]
        ~ playerIncorrectFrench++
        The voice guffaws. <i>Ah, le cus-de-mér may not always be right, non? Maison is most</i> definitely <i>le feminine.</i>
    ** [Don't you mean... <i>bienvenue à</i>? Not <i>bienvenue en</i>?]
        ~ playerCorrectFrench++
        You hear a metallic buzz and gurgle near the back of machine, like a refrigerator suddenly kicked into life. <i>Ah... mais that is what I said! Bienvenue </i>à<i> le Maison de Pomponville!</i>
                ***[Maybe this isn't worth it.]
                ***[But <i>maison</i> is feminine. Don't you mean <i>la Maison?]
                    The vending machine jolts. Click, click, click: then the quiet static again. <i>Ah, my dear cus-de-mér, I cannot 'ear you: there is so much... in-tér-fear-ance...</i>
                    ~ voiceKickedPlayerOut = true
                    **** [It doesn't seem that loud to you.]
                    -> Exit_Button
- 
{correctedVoicesFrench: The voice clears its throat.} <i>Well, uh, would you 'ave any vin to ensample?</i>

*Tu penses oui.
~ playerCorrectFrench++
->Order_Wine
*You think yes.
->Order_Wine
* You think not.
->Exit_Button
*Tu penses non.
~ playerCorrectFrench++
->Exit_Button

=Order_Wine
// choose a random wine type from the list to be served, as long as the customer hasn't said they want something of the same colour of wine.
{
- wantsDifferentWineColour == true:
    ~ askedForDifferentTypeOfWine = false
    ~ WineColours -= currentWineColour
    ~ currentWineColour = LIST_RANDOM(WineColours)
    ~ wantsDifferentWineColour = false
- ranOutOfWineTypes == true:
    ~ askedForDifferentTypeOfWine = false
    ~ WineColours -= currentWineColour
    ~ currentWineColour = LIST_RANDOM(WineColours)
    ~ wantsDifferentWineType = false
    ~ ranOutOfWineTypes = false
- wantsDifferentWineType == true:
    ~ wantsDifferentWineType = false
- else:
    ~ currentWineColour = LIST_RANDOM(WineColours)
    ~ WineColours -= currentWineColour
}

{LIST_COUNT(WineColours) > 0:
{<i>Sur bien!</i> The vending machine vibrates gently, then ends with a sudden record scratch. The static returns.|Another judder and hum from the machine.}

{A small hatch opens, revealing a spout not unlike an automatic coffee machine. A scratched |And another} plastic {wine glass falls down from above, and as teeters in place, {currentWineColour} wine gushes out of the spout to fill it.| glass precariously drops. This time, {currentWineColour} wine fills it.} 

<i>{Now, here is something|And here, this bev-vér-age is|I am certain that this time, this will be} very special:
} </i><>
{
- currentWineColour == red && LIST_COUNT(RedWineTypes) > 0:
    ~ currentWineType = LIST_RANDOM(RedWineTypes)
    ~ RedWineTypes -= currentWineType
    <i>a {nameOfRedWine(currentWineType)}.</i> <>

- currentWineColour  == white && LIST_COUNT(WhiteWineTypes) > 0:
    ~ currentWineType = LIST_RANDOM(WhiteWineTypes)
    ~ WhiteWineTypes -= currentWineType
    <i>a {nameOfWhiteWine(currentWineType)}.</i> <>

- currentWineColour == orange && LIST_COUNT(OrangeWineTypes) > 0:
    ~ currentWineType = LIST_RANDOM(OrangeWineTypes)
    ~ OrangeWineTypes -= currentWineType
    <i>a {nameOfOrangeWine(currentWineType)}.</i> <>

- currentWineColour == silver && LIST_COUNT(SilverWineTypes) > 0:
    ~ currentWineType = LIST_RANDOM(SilverWineTypes)
    ~ SilverWineTypes -= currentWineType
    <i>a {nameOfSilverWine(currentWineType)}.</i> <>
- else:
~ likesWine = false
The voice has gained a new quality: as if you can hear the sweat running down its brow. <i>Mais, my dear cus-de-mér... we 'ave no other types of wine.</i>
}

{likesWine:
    <i>Grown {nameOfRegionAdverbs(LIST_RANDOM(LIST_ALL(RegionAdverbs)))} in the {nameOfRegionDescriptors(LIST_RANDOM(LIST_ALL(RegionDescriptors)))} region of {nameOfPlaces(LIST_RANDOM(LIST_ALL(places)))}, this vintage has been aged in {nameOfBarrelAdjectives(LIST_RANDOM(BarrelAdjectives))} barrels for {nameOfNumberOfYears(LIST_RANDOM(NumberOfYears))} years. </i>
}
// [TODO add some logic to change the 'But' to 'On second thoughts' if the player has asked to see different type of same wine colour]
*{currentWineColour == silver || currentWineColour == orange}[What even... is {currentWineColour} wine?]
    {
    - currentWineColour == silver:
        <i>Ah, that is one of our</i> newer... <i> new world wines.</i> The voice offers no further explanation.
    - currentWineColour == orange:
        <i>I...</i> The voice sounds genuinely perplexed. <i>I thought this dimension had orange wine. Maybe you've just never had any?</i>
    }
    Do you want this glass of wine, or not?
    ++No, {currentWineColour} wine sounds weird.
    ~ wantsDifferentWineColour = false
    -> Reorder_Wine
    ++No, but you'd try a different type of {currentWineColour} wine.
    ~ wantsDifferentWineType = true
    -> AskForDifferentWineType
    ++Hell yeah, you'll have this {currentWineColour} wine, whatever it is.
    <i>Excellent!</i> The voice awaits eagerly as you take a sip.
    -> Taste_Wine
+{likesWine}{!askedForDifferentTypeOfWine}But you don't like {currentWineColour} wine.
~ wantsDifferentWineColour = true 
->Reorder_Wine
+{likesWine}{askedForDifferentTypeOfWine}On second thoughts, you don't like {currentWineColour} wine.
~ wantsDifferentWineColour = true 
->Reorder_Wine
+{likesWine}Actually... you would like to try a different type of {currentWineColour} wine.
~ wantsDifferentWineType = true
-> AskForDifferentWineType
+{likesWine}You bring the glass to your lips.
->Taste_Wine
+{!likesWine}Maybe you just don't like wine. Time to move on.
->Exit_Button

= AskForDifferentWineType
~ askedForDifferentTypeOfWine = true
{
    - (currentWineColour == red && LIST_COUNT(RedWineTypes) > 0) || (currentWineColour == white && LIST_COUNT(WhiteWineTypes) > 0) || (currentWineColour == orange && LIST_COUNT(OrangeWineTypes) > 0) || (currentWineColour == silver && LIST_COUNT(SilverWineTypes) > 0):
        <i>Mais of course!</i>
        ++ Excellent.
        -> Reorder_Wine
    - else:
        ~ ranOutOfWineTypes = true
        <i>Ah... oh... oh, non. We 'ave no more types of {currentWineColour}.</i>
        ++ A shame. Try something else.
        -> Reorder_Wine
        
        ++ No. You're done with wine.
        -> Exit_Button
}

=Reorder_Wine
// to do: add in some variation depending on if reordering because doesn't like colour or doesn't like type of wine

{
    - wantsDifferentWineColour == true:
    {A nervous chortle. <i>Le cus-de-mér is always right. If you please, just throw that, uh...</i>|Je suis</i> so <i>sorry, my dear cus-dé-mer: please, throw that|Once again, I can only apologise: fling that} {nameOfNegativeWineNouns(LIST_RANDOM(LIST_ALL(NegativeWineNouns)))} <i> on le floor. {Where it belongs.</i>|}
    - wantsDifferentWineType == false:
        <i>Ah, mais of course!</i>
    - ranOutOfWineTypes == true:
        ~ wantsDifferentWineType = true
        ~ wantsDifferentWineColour = false
}
-> Order_Wine

=Taste_Wine
~ currentWinesTasted++

// Later, if I want to make it so that all the adjectives are unique (or at least comment if they are repeated across wines), I can copy the below in order to record whether they have been chosen or not. Then, I can run if statement and check whether the noun is present in the list.
~ temp firstNoteNoun = ""
~ firstNoteNoun = LIST_RANDOM(NotesNoun)
~ NotesNoun -= firstNoteNoun

~ temp secondNoteNoun = ""
~ secondNoteNoun = LIST_RANDOM(NotesNoun)
~ NotesNoun -= secondNoteNoun

~ temp firstTanninsAdjective = ""
~ firstTanninsAdjective = LIST_RANDOM(TanninsAdjectives)
~ TanninsAdjectives -= firstTanninsAdjective

~ temp secondTanninsAdjective = ""
~ secondTanninsAdjective = LIST_RANDOM(TanninsAdjectives)
~ TanninsAdjectives -= secondTanninsAdjective

~ temp firstFlavourAdjective = ""
~ firstFlavourAdjective = LIST_RANDOM(FlavourAdjectives)
~ FlavourAdjectives -= firstFlavourAdjective

~ temp secondFlavourAdjective = ""
~ secondFlavourAdjective = LIST_RANDOM(FlavourAdjectives)
~ FlavourAdjectives -= secondFlavourAdjective

Hmm... how would you describe this? Notes of... {firstNoteNoun}, and {secondNoteNoun}. You might say the body feels {LIST_RANDOM(LIST_ALL(BodyAdjectives))}.

As it sits on your palate, it develops a new flavour. Something {firstFlavourAdjective}. Then, {secondFlavourAdjective}.

<i>Ahhh... my dear cus-de-mér, 'ow are you liking your wine? The tannins, they are delicious, no?</i>
+[They feel {firstTanninsAdjective}.]
    ->WineVerdict
+[You would say they're {secondTanninsAdjective}.]
    ->WineVerdict
    
=WineVerdict
<i>Ahh, I see...</i> You hear a pencil scribbling on paper. <i>And... your verdict? Does this wine live up to your expectations?</i>

+You love it.
+You think, eh.
+You had been waiting this whole time to spit it out.
    <i>Oh...</i> The voice has the distinct quality of someone looking at something like a squished insect on a dinner plate. <i>Ah, no. I knew we should have provided disposable spitoons as well...</i>

-
{currentWinesTasted < 3:
    <i>So: would you 'ave another?</i>
    +Yes, try another wine.
    ->Reorder_Wine
    +No. Time to line your stomach with something else.
    ->Exit_Button
- else:
    <i>My dear cus-de-mér, I would love to offer you another bev-ér-age, mais... the legal limit for this con-trap-tion is three wines.</i>
    +You suppress a hiccup. Perhaps it's good you're stopping here.
    ->Exit_Button
    +You feel totally fine, but whatever.
    ->Exit_Button
    +You could have had <i>twenty</i> more wines: what does this stupid voice know?!
    ->Exit_Button
}
=Exit_Button
{
- voiceKickedPlayerOut == true:
    <i>Non... ah non...</i> You hear a chair creak, as if the voice is leaning away from the microphone. <i>Non, je ne able pas to 'ear you... goodbye, my dear cus-de-mér...</i>
- currentWinesTasted > 0:
    The voice seems to sense your eyes drifting to other buttons. <i>Ah, I see you are leaving... please, uh, consider leaving a review of the wines you 'ave enjoyed on our webs-</i>
-else:
    The voice seems to sense your eyes drifting to other buttons. <i>Ah, o-kay, c'est... fine, mais, I do get paid by the number of reviews on our website, so please do-</i>
}

A pop, static, then a click. Silence. <>
{voiceKickedPlayerOut:The button is now unresponsive. }

The voice is gone.

->Vending_Machine_Entrance

=== function nameOfWhiteWine(what)
{ what:
    - savignonBlanc: savignon blanc
    - else: {what}
}

=== function nameOfRedWine(what)
{ what:
    // - shiraz: shiraz
    // - merlot: merlot
    - cabernetSauvignon: cabernet sauvignon
    - else: {what}
}

=== function nameOfOrangeWine(what)
{ what:
    - goûtDeGirofle: goût de girofle
}

=== function nameOfSilverWine(what)
{ what:
    // - boleau: boleau
    - pinotÉtoilé: pinot étoilé
    - else: {what}
}

=== function nameOfNegativeWineNouns(what)
{ what:
    - corkedRubbish: corked rubbish
    - rotgut: rot gut
    - else: {what}
}

=== function nameOfRegionAdverbs(what)
{ what:
    - ancientForest: ancientForest
    - greatPlains: greatPlains
    - else: {what}
}

=== function nameOfRegionDescriptors(what)
{what:
    - ancientForest: ancient forest
    - greatPlains: great plains
    - else: {what}
}

=== function nameOfPlaces(where)
{where:
    - surrey: Surrey
    - lunarMoon: a tiny lunar moon
    - newKensington: New Kensington
    - else: {where}
}

=== function nameOfBarrelAdjectives(what)
{what:
    - cherryWood: cherry wood
    - else: {what}
}

=== function nameOfNumberOfYears(howMany)
{howMany:
    - notSure: ...</i> The voice pauses. <i>Actually, I'm not sure for how many
    - unfathomableNumber: an unfathomable number of
    - else: {howMany}
}